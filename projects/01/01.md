# 01. 불 논리


## 수의 체계

---

# 패리티 비트(Parity bit)

---

`패리티 비트(Parity bit)`는 정보의 전달 과정에서 오류가 생겼는지를 검사하기 위해 추가된 비트이다. 문자열 내 1비트의 모든 숫자가 짝수 또는 홀수인지를 보증하기 위해 전송하고자 하는 데이터의 각 문자에 1 비트를 더하여 전송하는 방법으로 2가지 종류의 패리티 비트(홀수, 짝수)가 있다. 패리티 비트는 오류 검출 부호에서 가장 간단한 형태로 쓰인다.

- 짝수(even) 패리티는 전체 비트에서 1의 개수가 짝수가 되도록 패리티 비트를 정하는 것인데, 이를테면 데이터 비트에서 1의 개수가 홀수이면 패리티 비트를 1로 정한다. <u>이미 1의 개수가 짝수이면 패리티 비트를 0으로 정한다.</u>
- 홀수(odd) 패리티는 전체 비트에서 1의 개수가 홀수가 되도록 패리티 비트를 정하는 방법이다. <u>이미 1의 개수가 홀수라면 패리티 비트를 0으로 정한다.</u>

7비트의 0010110라는 데이터에서 짝수 패리티가 되게 하기 위해서는 1의 패리티 비트를 붙여 10010110로 한다. 또 같은 데이터에 대해 홀수 패리티 비트가 되게 하려면 0의 패리티 비트를 붙인다. 이렇게 패리티 비트를 정하여 데이터를 보내면 받는 쪽에서는 수신된 데이터의 전체 비트를 계산하여 패리티 비트를 다시 계산함으로써 데이터 오류 발생 여부를 알 수 있다. 그러나 패리티 비트는 오류 발생 여부만 알 수 있지 오류를 수정할 수는 없다.

<br />

# 진법(Notation)

---

인간들이 일상생활에 사용하는 `10진수(Decimal)`, 컴퓨터가 사용하는 `2진수(Binary)` 등.

진법은 단지 수의 체계에서 사용되는 수가 몇개인지를 말한다.

예를 들자면 10진수는 0~9의 숫자 10개를 사용, 2진수는 0과 1 2개의 숫자를 사용한다.

<br />

진수를 계산하는 방법은 기본적으로 모두 동일하다. 진수에 각 자릿수를 제곱하여 나온 값과 실제값을 곱한다.

이 때, 가장 낮은 자리는 0으로 시작한다.

이후 나온 값들을 모두 더한다.

<br />

`10진수 958`을 예시로 설명하면,
9는 100의자리, 5는 10의자리, 8은 1의 자리이므로 10에 각 자릿수를 지수로 넘겨준다.



> = 9x10^2 + 5x10^1 + 8x10^0
>
> = 9x100 + 5x10 + 8x1
>
> = 900 + 50 + 8
>
> = 958

<br />

소수점 이하는 -1부터 시작하며, 2진수, 8진수, 16진수 등도 동일하게 계산하면 된다.

<br />

# 보수(Complement)

---

`보수`란, `X와 X의 보수를 합산했을 때 진법의 밑수가 나오는 수`를 의미한다.

예를 들어 10진수 7의 보수는 3이고, 10진수 1의 보수는 9이다.

컴퓨터는 모든 연산을 `가산기(Adder)`만 사용해 처리한다. 즉, 모든 연산을 덧셈으로만 처리하는 셈이다.

따라서 뺄셈의 경우 보수가 필요해진다.

<br />

즉, `X-Y`를 `X+(-Y)`로 처리한다.

<br />

컴퓨터는 `2진수`를 사용하는데, `2진수의 보수`를 구하는 방법에 아래 두 가지 방법을 사용한다.

- **1의 보수**: 1을 0으로, 0을 1로 뒤집는다.
    - 1011의 1의 보수는 0100이다.

- **2의 보수**: 1의 보수를 구한 후 `최하위비트(LSB)`에 1을 더한다.
    - 1011의 2의 보수는 0101이다.
    - 사람이 알아보는데 약간 불편하다는 단점이 있다.
    - 성능이 가장 좋아 `부호있는비트`에서 가장 많이 사용된다.

<br />

## 보수의 표현

---

내가 사용하는 자바는 <u>int를 32비트의 부호있는 비트로 표현</u>하며, 자바에서 지원하는 `단항 NOT 연산자(~)`를 사용하면 `1의 보수`를 반환한다.

이를 코드로 나타내면 하기와 같다.

<br />

```java
System.out.println(~25); // -26
```

<br />

위 코드의 연산과정은 아래와 같다.

`25`는 32비트 2진수로 `0000 0000 0000 0000 0000 0000 0001 1001`이며, ~연산자를 적용하면 `1111 1111 1111 1111 1111 1111 1110 0110`이다.

반환된 `1의 보수(=음수로 표현된 2진수)`를 다시 10진수로 표현하려면 아래와 같은 과정을 거친다.

<br />

1. 1의 보수를 취한다. 즉, 0은 1로, 1은 0으로 변환하며 이 때 MSB도 같이 변환한다.
2. 1번 과정에서 나온 결과에 1을 더한다. (2의 보수)
3. 2번 과정에서 나온 결과를 10진수로 변환하고 -부호를 붙인다.

<br />

1번 과정을 진행하면 `1111 1111 1111 1111 1111 1111 1110 0110`의 1의 보수는 원래 값인 `0000 0000 0000 0000 0000 0000 0001 1001`이다.

2번 과정을 진행하면 LSB에 1을 더한 `0000 0000 0000 0000 0000 0000 0001 1010`이다.

3번 과정을 진행하면 `0000 0000 0000 0000 0000 0000 0001 1010`은 10진수 `26`이며, -부호를 붙여 `-26`이 나온다.

<br />

## 감산

---

- **1의 보수 감산**
    - 음수로 표현하려는 수의 1의 보수를 구한 후 더한다.
    - 덧셈 후 `최상위 비트(MSB)`에서 `자리올림(Carry)`이 생겼다면 `최하위 비트(LSB)`에 1을 더한다.
    - 자리올림이 생기지 않았다면 연산 결과에 대해 1의 보수를 구한 후 최상위비트에 `음수부호비트`를 붙인다.
- **2의 보수 감산**
    - 음수로 표현하려는 수의 2의 보수를 구한 후 더한다.
    - 덧셈 후 `최상위 비트(MSB)`에서 `자리올림(Carry)`이 생겼다면 자리올림을 버린 나머지 값이 연산 결과이다.
    - 자리올림이 생기지 않았다면 연산 결과에 대해 2의 보수를 구한 후 최상위비트에 `음수부호비트`를 붙인다.

<br />

![image](https://user-images.githubusercontent.com/71188307/139531667-7e290ae6-69eb-4aef-a33a-adae2959a45e.png)

<br />

## 실습방법

### 1. HardwareSimulator 실행

---

- `macOS`: `tools/HardwareSimulator.sh`를 실행합니다.
- `Windows`: `tools/HardwareSimulator.bat`을 실행합니다.

<br />

### 2. HardwareSimulator Run

---

![image](https://user-images.githubusercontent.com/71188307/133882348-202684f6-b950-49ed-9775-3b6d69f9c92c.png)

- `Load Chip` -> `{gate}.hdl`
- `Load Script` -> `{gate}.tst`
- `Run` 버튼 클릭
- `{gate}.out` 출력 확인 (`{gate}.cmp`랑 다를 경우 에러 발생)

<br />

## HDL API & Gate Design Reference

---

> 이 문서는 `Sean McCallum`님이 작성한 [문서](https://nand2tetris-hdl.github.io/) 를 번역한 것입니다. 

이 문서는 `nand2tetris` 과정(`밑바닥 부터 만드는 컴퓨팅 시스템` 기반)에 대한 API, 회로도 설계 및 HDL 구현에 대해 자세히 설명하며,

모든 HDL 구현은 하드웨어 시뮬레이터를 통해 테스트되었습니다.

> Note: 이 문서에서 회로도, 진리표 및 HDL은 적절한 경우에 한해 포함돼있습니다.

> Note: SOP, POS, 카르노맵에 대한 좀 더 자세한 정보는 [링크](https://optimuslee.tistory.com/entry/24-SOP%EA%B3%BC-POS-%EA%B7%B8%EB%A6%AC%EA%B3%A0-%EC%B9%B4%EB%A5%B4%EB%85%B8-%EB%A7%B5) 를 참고하세요.

### 목차

- [Not16](#Not16)
- [And16](#And16)
- [Or16](#Or16)
- [Or8Way](#Or8Way)
- [Mux](#Mux)
- [Mux16](#Mux16)
- [Mux4Way](#Mux4Way)
- [Mux4Way16](#Mux4Way16)
- [Mux8Way](#Mux8Way)
- [Mux8Way16](#Mux8Way16)
- [DMux](#DMux)
- [DMux4Way](#DMux4Way)
- [DMux8Way](#DMux8Way)

### Not16

---

|  Inputs  |  in\[16\]  |
|:--------:|:----------:|
|  Outputs |  out\[16\] |
|    SOP   |  !in\[i\]  |

```text
// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/01/Not16.hdl

/**
 * 16-bit Not:
 * for i=0..15: out[i] = not in[i]
 */

CHIP Not16 {
    IN in[16];
    OUT out[16];

    PARTS:
    Not(in=in[0], out=out[0]);
    Not(in=in[1], out=out[1]);
    Not(in=in[2], out=out[2]);
    Not(in=in[3], out=out[3]);
    Not(in=in[4], out=out[4]);
    Not(in=in[5], out=out[5]);
    Not(in=in[6], out=out[6]);
    Not(in=in[7], out=out[7]);
    Not(in=in[8], out=out[8]);
    Not(in=in[9], out=out[9]);
    Not(in=in[10], out=out[10]);
    Not(in=in[11], out=out[11]);
    Not(in=in[12], out=out[12]);
    Not(in=in[13], out=out[13]);
    Not(in=in[14], out=out[14]);
    Not(in=in[15], out=out[15]);
}
```

### And16

---

|  Inputs  |  a\[16\], b\[16\]  |
|:--------:|:------------------:|
|  Outputs |      out\[16\]     |
|    SOP   |         ab         |

```text
// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/01/And16.hdl

/**
 * 16-bit bitwise And:
 * for i = 0..15: out[i] = (a[i] and b[i])
 */

CHIP And16 {
    IN a[16], b[16];
    OUT out[16];

    PARTS:
    And(a=a[0], b=b[0], out=out[0]);Ŝ
    And(a=a[1], b=b[1], out=out[1]);
    And(a=a[2], b=b[2], out=out[2]);
    And(a=a[3], b=b[3], out=out[3]);
    And(a=a[4], b=b[4], out=out[4]);
    And(a=a[5], b=b[5], out=out[5]);
    And(a=a[6], b=b[6], out=out[6]);
    And(a=a[7], b=b[7], out=out[7]);
    And(a=a[8], b=b[8], out=out[8]);
    And(a=a[9], b=b[9], out=out[9]);
    And(a=a[10], b=b[10], out=out[10]);
    And(a=a[11], b=b[11], out=out[11]);
    And(a=a[12], b=b[12], out=out[12]);
    And(a=a[13], b=b[13], out=out[13]);
    And(a=a[14], b=b[14], out=out[14]);
    And(a=a[15], b=b[15], out=out[15]);
}
```

### Or8Way

---

|  Inputs  |                   in\[8\]                  |
|:--------:|:------------------------------------------:|
|  Outputs |                    out                     |
|    SOP   |         in\[0\]+in\[1\]+..+in\[7\]         |


![image](https://user-images.githubusercontent.com/71188307/134760235-926895b7-e62f-46ca-815b-96a97a0570cc.png)

```
// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/01/Or8Way.hdl

/**
 * 8-way Or:
 * out = (inin[0] or inin[1] or ... or inin[7])
 */

CHIP Or8Way {
    IN in[8];
    OUT out;

    PARTS:
    Or(a=in[0], b=in[1], out=or1);
    Or(a=in[2], b=in[3], out=or2);
    Or(a=in[4], b=in[5], out=or3);
    Or(a=in[6], b=in[7], out=or4);
    Or(a=or1, b=or2, out=or5);
    Or(a=or3, b=or4, out=or6);
    Or(a=or5, b=or6, out=out);
}
```

### Or16

---

|  Inputs  |       a\[16\], b\[16\]       |
|:--------:|:----------------------------:|
|  Outputs |           out\[16\]          |
|    SOP   |         a\[i\]+b\[i\]        |

```text
// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/01/Or16.hdl

/**
 * 16-bit bitwise Or:
 * for i = 0..15 out[i] = (a[i] or b[i])
 */

CHIP Or16 {
    IN a[16], b[16];
    OUT out[16];

    PARTS:
    Or(a=a[0], b=b[0], out=out[0]);
    Or(a=a[1], b=b[1], out=out[1]);
    Or(a=a[2], b=b[2], out=out[2]);
    Or(a=a[3], b=b[3], out=out[3]);
    Or(a=a[4], b=b[4], out=out[4]);
    Or(a=a[5], b=b[5], out=out[5]);
    Or(a=a[6], b=b[6], out=out[6]);
    Or(a=a[7], b=b[7], out=out[7]);
    Or(a=a[8], b=b[8], out=out[8]);
    Or(a=a[9], b=b[9], out=out[9]);
    Or(a=a[10], b=b[10], out=out[10]);
    Or(a=a[11], b=b[11], out=out[11]);
    Or(a=a[12], b=b[12], out=out[12]);
    Or(a=a[13], b=b[13], out=out[13]);
    Or(a=a[14], b=b[14], out=out[14]);
    Or(a=a[15], b=b[15], out=out[15]);
}
```

### Mux

---

|  Inputs  |      a, b, sel     |
|:--------:|:------------------:|
|  Outputs |         out        |
|    SOP   |        s̄a+s̄b       |

|  sel  |   out  |
|:-----:|:------:|
|   o   |    a   |
|   o   |    b   |


![image](https://user-images.githubusercontent.com/71188307/134760365-86652ccf-40ae-427f-94d3-8f8982852b53.png)

```text
// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/01/Mux.hdl

/**
 * Multiplexor:
 * out = a if sel == 0
 *       b otherwise
 */

CHIP Mux {
    IN a, b, sel;
    OUT out;

    PARTS:
    Not(in=sel, out=notSel);
    And(a=a, b=notSel, out=andA);
    And(a=sel, b=b, out=andB);
    Or(a=andA, b=andB, out=out);
}
```

### Mux16

---

|  Inputs  |    a\[16\], b\[16\], sel    |
|:--------:|:---------------------------:|
|  Outputs |          out\[16\]          |

```text
// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/01/Mux16.hdl

/**
 * 16-bit multiplexor:
 * for i = 0..15 out[i] = a[i] if sel == 0
 *                        b[i] if sel == 1
 */

CHIP Mux16 {
    IN a[16], b[16], sel;
    OUT out[16];

    PARTS:
    Mux(a=a[0], b=b[0], sel=sel, out=out[0]);
    Mux(a=a[1], b=b[1], sel=sel, out=out[1]);
    Mux(a=a[2], b=b[2], sel=sel, out=out[2]);
    Mux(a=a[3], b=b[3], sel=sel, out=out[3]);
    Mux(a=a[4], b=b[4], sel=sel, out=out[4]);
    Mux(a=a[5], b=b[5], sel=sel, out=out[5]);
    Mux(a=a[6], b=b[6], sel=sel, out=out[6]);
    Mux(a=a[7], b=b[7], sel=sel, out=out[7]);
    Mux(a=a[8], b=b[8], sel=sel, out=out[8]);
    Mux(a=a[9], b=b[9], sel=sel, out=out[9]);
    Mux(a=a[10], b=b[10], sel=sel, out=out[10]);
    Mux(a=a[11], b=b[11], sel=sel, out=out[11]);
    Mux(a=a[12], b=b[12], sel=sel, out=out[12]);
    Mux(a=a[13], b=b[13], sel=sel, out=out[13]);
    Mux(a=a[14], b=b[14], sel=sel, out=out[14]);
    Mux(a=a[15], b=b[15], sel=sel, out=out[15]);
}
```

### Mux4Way

---

|  Inputs  |     a, b, c, d, sel\[2\]    |
|:--------:|:---------------------------:|
|  Outputs |            out              |

|  sel\[0\]  |   sel\[1\]   |    out   |
|:----------:|:------------:|:--------:|
|     0      |       0      |     a    |
|     0      |       1      |     b    |
|     1      |       0      |     c    |
|     1      |       1      |     d    |

![image](https://user-images.githubusercontent.com/71188307/134760524-07cc1042-c50a-4b6d-8413-9e8bb8bebb95.png)

### Mux4Way16

---

|  Inputs  |     a\[16\], b\[16\], sel\[2\]    |
|:--------:|:---------------------------------:|
|  Outputs |            out\[16\]              |

```text
// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/01/Mux4Way16.hdl

/**
 * 4-way 16-bit multiplexor:
 * out = a if sel == 00
 *       b if sel == 01
 *       c if sel == 10
 *       d if sel == 11
 */

CHIP Mux4Way16 {
    IN a[16], b[16], c[16], d[16], sel[2];
    OUT out[16];

    PARTS:
    Mux16(a=a, b=b, sel=sel[0], out=muxAB);
    Mux16(a=c, b=d, sel=sel[0], out=muxCD);
    Mux16(a=muxAB, b=muxCD, sel=sel[1], out=out);
}
```

### Mux8Way

---

|  Inputs  |     a, b, c, d, e, f, g, h, sel\[3\]    |
|:--------:|:---------------------------------------:|
|  Outputs |                    out                  |

|  sel\[0\]  |   sel\[1\]   |   sel\[2\]   |    out   |
|:----------:|:------------:|:------------:|:--------:|
|     0      |       0      |       0      |     a    |
|     0      |       1      |       0      |     b    |
|     1      |       0      |       0      |     c    |
|     1      |       1      |       0      |     d    |
|     0      |       0      |       1      |     e    |
|     0      |       1      |       1      |     f    |
|     1      |       0      |       1      |     g    |
|     1      |       1      |       1      |     h    |

![image](https://user-images.githubusercontent.com/71188307/134760710-96c356aa-2688-4506-9e5e-d7da48dcf4c1.png)

### Mux8Way16

---

|  Inputs  |     a\[16\], b\[16\], c\[16\], d\[16\], e\[16\], f\[16\], g\[16\], h\[16\], sel\[3\]    |
|:--------:|:---------------------------------------------------------------------------------------:|
|  Outputs |                                          out                                            |

```text
// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/01/Mux8Way16.hdl

/**
 * 8-way 16-bit multiplexor:
 * out = a if sel == 000
 *       b if sel == 001
 *       etc.
 *       h if sel == 111
 */

CHIP Mux8Way16 {
    IN a[16], b[16], c[16], d[16],
       e[16], f[16], g[16], h[16],
       sel[3];
    OUT out[16];

    PARTS:
    Mux4Way16(a=a, b=b, c=c, d=d, sel=sel[0..1], out=muxAD);
    Mux4Way16(a=e, b=f, c=g, d=h, sel=sel[0..1], out=muxEH);
    Mux16(a=muxAD, b=muxEH, sel=sel[2], out=out);
}
```

### DMux

---

|  Inputs  |       in, sel      |
|:--------:|:------------------:|
|  Outputs |        a, b        |
|    SOP   |      a=xs̄, b=xs    |

|  sel  |   a   |   b   |
|:-----:|:-----:|:-----:|
|   0   |   in  |   0   |
|   1   |   0   |  in   |

![image](https://user-images.githubusercontent.com/71188307/134760806-ab7e0d77-e88e-451f-9f64-876bbe5b7268.png)

```text
// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/01/DMux.hdl

/**
 * Demultiplexor:
 * {a, b} = {in, 0} if sel == 0
 *          {0, in} if sel == 1
 */

CHIP DMux {
    IN in, sel;
    OUT a, b;

    PARTS:
    Not(in=sel, out=notSel);
    And(a=notSel, b=in, out=a);
    And(a=in, b=sel, out=b);
}
```

### DMux4Way

---

|   Inputs   |    in, sel\[2\]  |
|:----------:|:----------------:|
|   Outputs  |     a, b, c, d   |

| sel\[0\] | sel\[1\] |   a   |   b   |   c   |   d   |
|:--------:|:--------:|:-----:|:-----:|:-----:|:-----:|
|    0     |     0    |  in   |   0   |   0   |   0   |
|    0     |     1    |   0   |  in   |   0   |   0   |
|    1     |     0    |   0   |   0   |  in   |   0   |
|    1     |     1    |   0   |   0   |   0   |  in   |

![image](https://user-images.githubusercontent.com/71188307/134761076-a3306430-900b-4195-aefe-c308a752ec38.png)

```text
// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/01/DMux4Way.hdl

/**
 * 4-way demultiplexor:
 * {a, b, c, d} = {in, 0, 0, 0} if sel == 00
 *                {0, in, 0, 0} if sel == 01
 *                {0, 0, in, 0} if sel == 10
 *                {0, 0, 0, in} if sel == 11
 */

CHIP DMux4Way {
    IN in, sel[2];
    OUT a, b, c, d;

    PARTS:
    DMux(in=in, sel=sel[1], a=dIn0, b=dIn1);
    DMux(in=dIn0, sel=sel[0], a=a, b=b);
    DMux(in=dIn1, sel=sel[0], a=c, b=d);
}
```

### DMux8Way

---

|  Inputs   |         in, sel\[3\]          |
|:---------:|:-----------------------------:|
|  Outputs  |  a, b, c, d, e, f, g, h, out  |

|  sel\[0\]  |  sel\[1\]  |  sel\[2\]  |  out  |
|:----------:|:----------:|:----------:|:-----:|
|      0     |      0     |      0     |   a   |
|      0     |      1     |      0     |   b   |
|      1     |      0     |      0     |   c   |
|      1     |      1     |      0     |   d   |
|      0     |      0     |      1     |   e   |
|      0     |      1     |      1     |   f   |
|      1     |      0     |      1     |   g   |
|      1     |      1     |      1     |   h   |

> Note: this table is shortened.

![image](https://user-images.githubusercontent.com/71188307/134761203-c618a640-c419-4217-926e-b70dcbde3764.png)

```text
// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/01/DMux8Way.hdl

/**
 * 8-way demultiplexor:
 * {a, b, c, d, e, f, g, h} = {in, 0, 0, 0, 0, 0, 0, 0} if sel == 000
 *                            {0, in, 0, 0, 0, 0, 0, 0} if sel == 001
 *                            etc.
 *                            {0, 0, 0, 0, 0, 0, 0, in} if sel == 111
 */

CHIP DMux8Way {
    IN in, sel[3];
    OUT a, b, c, d, e, f, g, h;

    PARTS:
    DMux(in=in, sel=sel[2], a=dIn0, b=dIn1);
    DMux4Way(in=dIn0, sel=sel[0..1], a=a, b=b, c=c, d=d);
    DMux4Way(in=dIn1, sel=sel[0..1], a=e, b=f, c=g, d=h);
}
```

<br />
